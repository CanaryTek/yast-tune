/**
 * File:
 *   system_settings.ycp
 *
 * Summary:
 *   Configuration of System Settings. PCI ID, Kernel parameters,
 *   Bootloader parameters etc.
 *
 * Authors:
 *   Lukas Ocilka <locilka@suse.cz>
 *
 * $Id$
 *
 */

{

    import "CWM";
    import "CWMTab";
    import "Label";
    import "Arch";
    import "Wizard";
    import "Mode";

    textdomain "tune";

    term contents = `VBox ( "tab" );

    map tabs_descr = $[
	"pci_id" : $[
	    "header" : NewPCIIDDialogCaption(),
	    "contents" : `HBox (
		`HSpacing(1),
		`VBox (
		    `VSpacing(0.3),
		    "pci_id_table_and_buttons",
		    `VSpacing(0.3)
		),
		`HSpacing(1)
	    ),
	    "widget_names" : ["pci_id_table_and_buttons"],
	],
	"kernel_settings" : $[
	    "header" : _("Kernel Settings"),
	    "contents" : `HBox (
		`HSpacing(1),
		`VBox (
		    `VSpacing(0.3),
		    `Left( "elevator" ),
		    `VSpacing(1),
		    `Left( "sysrq" )
		),
		`HSpacing (1)
	    ),
	    "widget_names" : ["elevator", "sysrq"],
	],
    ];

    string initial_tab = "pci_id";

    list <string> widget_names = ["tab"];
    map <string, map <string, any> > widget_descr = $[
	"pci_id_table_and_buttons" : $[
	    "widget" : `custom,
	    "custom_widget" : NewPCIIDDialogContent(),
	    "help" : NewPCIIDDialogHelp(),
	    "init" : InitNewPCIIDDialog,
	    "handle" : HandleNewPCIIDDialog,
	],

	/* /usr/src/linux/Documentation/kernel-parameters.txt
	 * http://www.redhat.com/magazine/008jun05/features/schedulers/
	 *
	 * elevator=    [IOSCHED]
	 *		Format: {"cfq"|"deadline"|"noop"}
	 *		See Documentation/block/as-iosched.txt
	 *		and Documentation/block/deadline-iosched.txt for details.
	 *
	 *	'deadline' =>	Deadline. Database servers, especially those using "TCQ" disks should
	 *			investigate performance with the 'deadline' IO scheduler. Any system with high
	 *			disk performance requirements should do so, in fact.
	 *	'noop' => NOOP
	 *	'cfq' => Completely Fair Queuing (the default)
	 */
	"elevator" : $[
	    "widget" : `custom,
	    // combo box label
	    "custom_widget" : `ComboBox (`id("elevator"), _("Global &I/O Scheduler"), [
		// combo box item - I/O scheduler
		`item( `id(""),	_("Not Configured")),
		// combo box item - I/O scheduler, do not translate the abbreviation in brackets
		`item( `id("cfq"),	_("Completely Fair Queuing [cfq]")),
		// combo box item - I/O scheduler, do not translate the abbreviation in brackets
		`item( `id("noop"),	_("NOOP [noop]")),
		// combo box item - I/O scheduler, do not translate the abbreviation in brackets
		`item( `id("deadline"),	_("Deadline [deadline]"))
	    ]),
	    "handle" : HandleElevatorSettings,
	    "init" : InitElevatorSettings,
	    "store" : StoreElevatorSettings,
	    // help text for the scheduler widget, do not translate 'cfg'
	    "help" : _("<p><b><big>Global I/O Scheduler</big></b><br>
It is possible to select the algorithm which orders and sends commands to disk
devices. This is a global option, it will be used for all disk devices in the
system. If the option is not configured the default (usually 'cfq') scheduler
will be used. See the documentation in the /usr/src/linux/Documentation/block
directory (package kernel-source) for more information.</p>
"),
	],
	// .sysconfig.sysctl
	"sysrq" : $[
	    "widget" : `checkbox,
	    "label" : _("Enable &SysRq Keys"),
	    "store" : StoreSysRqSettings,
	    "init" : InitSysRqSettings,
	    // TRANSLATORS: Help text - over taken from /etc/sysconfig/sysctl file
	    "help" : _("<p><b><big>Enable SysRq Keys</big></b><br>
If you enable SysRq keys, you will have some control over the system even if it
crashes (such as during kernel debugging). If it is enabled, the key combination
Alt-SysRq-<command_key> will start the respective command (e.g. reboot the
computer, dump kernel information). For further information, see
<tt>/usr/src/linux/Documentation/sysrq.txt</tt> (package kernel-source).</p>
")
	    ],
    ];

    define symbol SystemSettingsDialog () {
	list<string> tab_order = ["kernel_settings"];

	// do not show PCI ID tab on s390
	if (!Arch::s390())
	{
	    tab_order = prepend(tab_order, "pci_id");
	}
	else
	{
	    // remove the PCI ID tab definition
	    tabs_descr = remove(tabs_descr, "pci_id");

	    // set focus to kernel_settings tab
	    initial_tab = "kernel_settings";
	}

	widget_descr["tab"] = CWMTab::CreateWidget($[
	    "tab_order": tab_order,
	    "tabs": tabs_descr,
	    "widget_descr": widget_descr,
	    "initial_tab" : initial_tab,
	]);

	// explicitly set no help (otherwise CWM logs an error)
	widget_descr["tab","help"] = "";

	string caption = _("Kernel Settings");

	Wizard::SetContentsButtons("", `VBox (), "",
	    Label::BackButton(), Label::NextButton());
	Wizard::DisableBackButton();
	Wizard::SetDesktopIcon("powertweak");

	symbol ret = CWM::ShowAndRun ($[
	    "widget_descr" : widget_descr,
	    "widget_names" : widget_names,
	    "contents" : contents,
	    "caption" : caption,
	    // hide back button
	    "back_button" : nil,
	    "abort_button" : Label::CancelButton(),
	    "next_button" : Label::OKButton(),
	]);

	if (ret != `back && ret != `abort && ret != `cancel) {
	    initial_tab = CWMTab::CurrentTab ();
	}
	y2milestone("Returning %1", ret);

	return ret;
    }
}
